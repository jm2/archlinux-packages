# Maintainer: John-Michael Mulesa <jmulesa [at] gmail.com>

pkgname=gridautosport-steam
_pkgname=gridautosport
pkgver=1.0
pkgrel=1
_steamid=255220
arch=('i686' 'x86_64')
makedepends=('steamcmd')
depends=('openssl-1.0' 'libidn11' 'librtmp0' 'gconf')
conflicts=('gridautosport-wine-steam')
source=("${_pkgname}.sh" "${_pkgname}.desktop"
        "Goldberg_Lan_Steam_Emu_master--a24a9c26.zip::https://gitlab.com/Mr_Goldberg/goldberg_emulator/-/jobs/1590301114/artifacts/download")
pkgdesc="GRID Autosport Linux-native with data via Steam"
license=('custom')
url='https://steamdb.info/app/255220/'
sha256sums=('5c1f1f8294fa53f96cdadd01a4dd416e67cf24f52527e3e416320f5253393d0d'
            '0bde240c2d9a92e9f09fb3304acf29c59e78c4ff9bf4f99a066041865e3b489a'
            '3bfe422a3daf85a02fe6458e4351a4f40f29f513ec8df2c838c7e197844e51cb')

prepare() {
    mkdir -p $srcdir/${_pkgname}

    # Use steamcmd to get data.
    printf "Enter your Steam username:"
    read steam_username
    steamcmd +@sSteamCmdForcePlatformType linux +@ShutdownOnFailedCommand 1 +force_install_dir $srcdir/${_pkgname} +login $steam_username "+app_update ${_steamid} validate" +quit
}

package() {
    mkdir -p $pkgdir/opt/${_pkgname}

    # Move required files to pkgdir
    cp -r $srcdir/${_pkgname}/* $pkgdir/opt/${_pkgname}/
    rm -rf $pkgdir/opt/${_pkgname}/steamapps
    $srcdir/linux/tools/find_interfaces.sh $pkgdir/opt/${_pkgname}/lib/x86_64/libsteam_api.so > $pkgdir/opt/${_pkgname}/lib/x86_64/steam_interfaces.txt || true
    cp -f $srcdir/linux/x86_64/libsteam_api.so $pkgdir/opt/${_pkgname}/lib/x86_64/libsteam_api.so
    echo "${_steamid}" > $pkgdir/opt/${_pkgname}/lib/x86_64/steam_appid.txt
    sed -i 's/. "${GAMEROOT}\/config\/steam-check.sh"//' $pkgdir/opt/${_pkgname}/GridAutosport.sh

    # Install desktop file.
    install -D -m 644 $srcdir/${_pkgname}.desktop \
             $pkgdir/usr/share/applications/${_pkgname}.desktop

    # Install icon file.
    install -D -m 644 $srcdir/${_pkgname}/share/GameIcons/icon_512x512.png \
             $pkgdir/usr/share/pixmaps/${_pkgname}.png

    # Install bash startup script.
    install -D -m 755 $srcdir/${_pkgname}.sh \
             $pkgdir/usr/bin/${_pkgname}
}
