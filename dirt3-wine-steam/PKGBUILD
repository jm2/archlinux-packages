# Maintainer: John-Michael Mulesa <jmulesa [at] gmail.com>

pkgname=dirt3-wine-steam
_pkgname=dirt3
pkgver=1.1
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('steamcmd' 'icoutils')
depends=('wine')
source=("${_pkgname}.sh" "${_pkgname}.desktop"
        "https://github.com/ThirteenAG/Ultimate-ASI-Loader/releases/download/v4.59/Ultimate-ASI-Loader.zip")
pkgdesc="DiRT 3 using WINE with data via Steam"
license=('custom')
install=dirt3.install
url='https://steamdb.info/app/44320/'
sha256sums=('70f86e6c3783ce6734576b313ade781a5e2b85654421562d0058f2e73cf73dc1'
            '39174952df3585de3c2cdd0d3dd89ea22686e639b5a6aebb107832c7ee56572c'
            'ee55665eceef3ca18b5a0bafe269d41cd814e34d572aa1083c4b78b85d5b3958')

prepare() {
    mkdir -p $srcdir/${_pkgname}

    # Use steamcmd to get data.
    printf "Enter your Steam username:"
    read steam_username
    steamcmd +@sSteamCmdForcePlatformType windows +@ShutdownOnFailedCommand 1 +force_install_dir $srcdir/${_pkgname} +login $steam_username "+app_update 44320 validate" +quit
    wrestool -x -t -14 -o . ${_pkgname}/${_pkgname}.exe
    icotool -x -o . *.ico
}

package() {
    mkdir -p $pkgdir/opt/${_pkgname}

    # Move required files to pkgdir
    cp -r $srcdir/${_pkgname}/* $pkgdir/opt/${_pkgname}/
    cp $srcdir/dinput8.dll $pkgdir/opt/${_pkgname}/xlive.dll
    rm -rf $pkgdir/opt/${_pkgname}/steamapps
    
    # Install desktop file.
    install -D -m 644 $srcdir/${_pkgname}.desktop \
             $pkgdir/usr/share/applications/${_pkgname}.desktop

    # Install icon file.
    install -D -m 644 $srcdir/dirt3.exe_14_MAINICON_0_1_256x256x32.png \
             $pkgdir/usr/share/pixmaps/${_pkgname}.png

    # Install bash startup script.
    install -D -m 755 $srcdir/${_pkgname}.sh \
             $pkgdir/usr/bin/${_pkgname}
}
