# Maintainer: John-Michael Mulesa <jmulesa at gmail dot com>
# Contributor: kyndair <kyndair at gmail dot com>

pkgname=doom3bfg-data-gog
_pkgname=doom3bfg
pkgver=1
_pkgver="1.14_(13452)"
pkgrel=2
pkgdesc="Doom 3 BFG data via GOG"
arch=('any')
url="https://www.gog.com/game/doom_3_bfg_edition"
license=('custom')
makedepends=('innoextract' 'icoutils')
provides=('doom3bfg-data')
conflicts=('doom3bfg-data-steam')
source=("setup_doom_3_bfg_${_pkgver}.exe::gogdownloader://doom_3_bfg_edition_game/en1installer0" "setup_doom_3_bfg_${_pkgver}-1.bin::gogdownloader://doom_3_bfg_edition_game/en1installer1")
sha256sums=('4d09d3d11fca9d5122fc9af9aa12b8701742940b393b9bf63a4d9cdd825c4135'
            '9d163dbf728f8e2c00d26ac8de29a8b8e3bf1111d45ab320dea6371aaedd32eb')

# If you want to use lgogdownloader add the following to /etc/makepkg.conf
# DLAGENTS+=('gogdownloader::/usr/bin/lgogdownloader --download-file=%u -o %o')
DLAGENTS+=("gogdownloader::/usr/bin/echo Could not find gog installer file (%u). Manually download it to \"$(pwd)\", or set up a gogdownloader:// DLAGENT in /etc/makepkg.conf. See PKGBUILD for example.")

prepare() {
    innoextract -s "setup_doom_3_bfg_${_pkgver}.exe"
    icotool -x -o . app/goggame-*.ico
}

package() {
    mkdir -p $pkgdir/opt/${_pkgname}/base

    # install game icon
    install -Dm644 "${srcdir}/goggame-1836059896_7_128x128x32.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}-gog.png"

    # copy data structure
    cp -r $srcdir/app/base/* $pkgdir/opt/${_pkgname}/base/
}
