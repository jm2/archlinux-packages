# Maintainer: John-Michael Mulesa <jmulesa at gmail dot com>

pkgname=grid-wine-steam
_pkgname=grid
pkgver=1.3
pkgrel=1
pkgdesc="Race Driver GRID (2008) using WINE with data via Steam"
arch=('i686' 'x86_64')
url="https://steamdb.info/app/12750/"
license=('custom')
install=grid.install
depends=('wine')
depends_i686=('openal')
depends_x86_64=('lib32-openal')
conflicts=('grid-wine-gog')
makedepends=('steamcmd' 'icoutils')
source=("${_pkgname}.desktop" "${_pkgname}.sh" "hardware_settings_restrictions.xml")
sha256sums=('9e5f6161d864adc71dc42b27985ee167d39164f3e4338b865e97e35d619407cd'
            'a274a8674ee847d859d854c96cb715b46f53b599c172b34799461eeed76b4184'
            '05a812157a3d99608236b2e276b6d77959ed93295a7d18fad9cd242f82aa21d2')

prepare() {
    mkdir -p $srcdir/${_pkgname}

    # Use steamcmd to get data.
    printf "Enter your Steam username:"
    read steam_username
    steamcmd +@sSteamCmdForcePlatformType windows +@ShutdownOnFailedCommand 1 +force_install_dir $srcdir/${_pkgname} +login $steam_username "+app_update 12750 validate" +quit
    icotool -x -o . ${_pkgname}/*.ico
}

package() {
    mkdir -p $pkgdir/opt/${_pkgname}

    # Move required files to pkgdir
    cp -r $srcdir/${_pkgname}/* $pkgdir/opt/${_pkgname}/
    cp $srcdir/hardware_settings_restrictions.xml $pkgdir/opt/${_pkgname}/system/
    rm -rf $pkgdir/opt/${_pkgname}/steamapps
    rm $pkgdir/opt/${_pkgname}/installscript.vdf
    # Note that this version doesn't currently work due to Steam DRM
    # in GRID.exe. Unfortunately the retail 1.3 patch still requires
    # a DVD as well, so you'll have to source a 1.3-patched GRID.exe
    
    # Install desktop file.
    install -D -m 644 $srcdir/${_pkgname}.desktop \
             $pkgdir/usr/share/applications/${_pkgname}.desktop

    # Install icon file.
    install -D -m 644 $srcdir/codies_1_256x256x32.png \
             $pkgdir/usr/share/pixmaps/${_pkgname}.png

    # Install bash startup script.
    install -D -m 755 $srcdir/${_pkgname}.sh \
             $pkgdir/usr/bin/${_pkgname}
}
