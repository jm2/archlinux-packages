# Maintainer: John-Michael Mulesa <jmulesa [at] gmail.com>

pkgname=splitsecond-wine-steam
_pkgname=splitsecond
pkgver=1.0
pkgrel=1
arch=('i686' 'x86_64')
_steamid=297860
makedepends=('steamcmd')
depends=('wine')
source=("${_pkgname}.sh" "${_pkgname}.desktop" "${_pkgname}.png")
pkgdesc="Split/Second using WINE with data via Steam"
license=('custom')
url='https://store.steampowered.com/app/297860/SplitSecond/'
sha256sums=('2de6069313ac922f67ebc99ab284d125cf0f6da0e8848763dedf706327ad5d39'
            'a6158993848aed118e09b676e3df85cf5a45aa33ab76fa07a66d46bf5c652c4a'
            'd30e3ea9313de07dde53a913cb60270d9ec51acfd2b2d8bef6df0b7ed35ee38b')

prepare() {
    mkdir -p $srcdir/${_pkgname}

    # Use steamcmd to get data.
    printf "Enter your Steam username:"
    read steam_username
    steamcmd +@sSteamCmdForcePlatformType windows +@ShutdownOnFailedCommand 1 +force_install_dir $srcdir/${_pkgname} +login $steam_username "+app_update ${_steamid} validate" +quit
}

package() {
    mkdir -p $pkgdir/opt/${_pkgname}

    # Move required files to pkgdir
    cp -r $srcdir/${_pkgname}/* $pkgdir/opt/${_pkgname}/
    rm -rf $pkgdir/opt/${_pkgname}/steamapps

    # Install desktop file.
    install -D -m 644 $srcdir/${_pkgname}.desktop \
             $pkgdir/usr/share/applications/${_pkgname}.desktop

    # Install icon file.
    install -D -m 644 $srcdir/${_pkgname}.png \
             $pkgdir/usr/share/pixmaps/${_pkgname}.png

    # Install bash startup script.
    install -D -m 755 $srcdir/${_pkgname}.sh \
             $pkgdir/usr/bin/${_pkgname}
}
