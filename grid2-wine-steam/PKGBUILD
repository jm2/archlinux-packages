# Maintainer: John-Michael Mulesa <jmulesa [at] gmail.com>

pkgname=grid2-wine-steam
_pkgname=grid2
pkgver=1.0.85
pkgrel=1
_steamid=44350
arch=('i686' 'x86_64')
makedepends=('steamcmd' 'icoutils')
depends=('wine' 'winetricks')
source=("${_pkgname}.sh" "${_pkgname}.desktop" "${_pkgname}.exe" "${_pkgname}_avx.exe"
        "Goldberg_Lan_Steam_Emu_master--a24a9c26.zip::https://gitlab.com/Mr_Goldberg/goldberg_emulator/-/jobs/1590301114/artifacts/download")
pkgdesc="GRID 2 Reloaded Edition using WINE with data via Steam"
license=('custom')
install=grid2.install
url='https://steamdb.info/app/44350/'
sha256sums=('7458ac00f76e8b56206d06cf47d814e1b64772d402875a2e05f553b78ab4d01f'
            'd34065e56a9009ac09d9bc09fe4121024a4de46ab4a80c45b7e76c15e0169633'
            'd8ec0c631b453d6e5e9adebae4cfc9b612823fec316344c28544bc65245ba35d'
            '825f30a49a668870c7bfab0309c3afb01a396fc1cec0be8df9764e32bf2fb53f'
            '3bfe422a3daf85a02fe6458e4351a4f40f29f513ec8df2c838c7e197844e51cb')

prepare() {
    mkdir -p $srcdir/${_pkgname}

    # Use steamcmd to get data.
    printf "Enter your Steam username:"
    read steam_username
    steamcmd +@sSteamCmdForcePlatformType windows +@ShutdownOnFailedCommand 1 +force_install_dir $srcdir/${_pkgname} +login $steam_username "+app_update ${_steamid} validate" +quit
    wrestool -x -t -14 -o . ${_pkgname}/${_pkgname}.exe
    icotool -x -o . *.ico
}

package() {
    mkdir -p $pkgdir/opt/${_pkgname}

    # Move required files to pkgdir
    cp -r $srcdir/${_pkgname}/* $pkgdir/opt/${_pkgname}/
    rm -rf $pkgdir/opt/${_pkgname}/steamapps
    $srcdir/linux/tools/find_interfaces.sh $pkgdir/opt/${_pkgname}/steam_api64.dll > $pkgdir/opt/${_pkgname}/steam_interfaces.txt || true
    cp $srcdir/steam_api.dll $pkgdir/opt/${_pkgname}/
    cp $srcdir/${_pkgname}{,_avx}.exe $pkgdir/opt/${_pkgname}/
    echo "${_steamid}" > $pkgdir/opt/${_pkgname}/steam_appid.txt

    # Install desktop file.
    install -D -m 644 $srcdir/${_pkgname}.desktop \
             $pkgdir/usr/share/applications/${_pkgname}.desktop

    # Install icon file.
    install -D -m 644 $srcdir/${_pkgname}.exe_*_256x256x32.png \
             $pkgdir/usr/share/pixmaps/${_pkgname}.png

    # Install bash startup script.
    install -D -m 755 $srcdir/${_pkgname}.sh \
             $pkgdir/usr/bin/${_pkgname}
}
