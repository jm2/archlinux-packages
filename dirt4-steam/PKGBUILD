# Maintainer: John-Michael Mulesa <jmulesa [at] gmail.com>

pkgname=dirt4-steam
_pkgname=dirt4
pkgver=1.12
pkgrel=1
_steamid=421020
arch=('x86_64')
makedepends=('steamcmd')
depends=('openssl-1.0' 'libidn11' 'librtmp0' 'gconf')
conflicts=('dirt4-wine-steam')
source=("${_pkgname}.sh" "${_pkgname}.desktop"
        "Goldberg_Lan_Steam_Emu_master--a24a9c26.zip::https://gitlab.com/Mr_Goldberg/goldberg_emulator/-/jobs/1590301114/artifacts/download")
pkgdesc="DiRT 4 Linux-native with data via Steam"
license=('custom')
url='https://store.steampowered.com/app/421020/DiRT_4/'
sha256sums=('c3e15d41ee0bba6e755e6d71dbbce39d293dd0be7943db0bda28e288f4614212'
            'b837cedc7ccc0e8091269d4f33b852bf0ddb62910633c7e9f78905fe63552b8a'
            '3bfe422a3daf85a02fe6458e4351a4f40f29f513ec8df2c838c7e197844e51cb')

prepare() {
    mkdir -p $srcdir/${_pkgname}

    # Use steamcmd to get data.
    printf "Enter your Steam username:"
    read steam_username
    steamcmd +@sSteamCmdForcePlatformType linux +@ShutdownOnFailedCommand 1 +force_install_dir $srcdir/${_pkgname} +login $steam_username "+app_update ${_steamid} validate" +quit
}

package() {
    mkdir -p $pkgdir/opt/${_pkgname}

    # Move required files to pkgdir
    cp -r $srcdir/${_pkgname}/* $pkgdir/opt/${_pkgname}/
    rm -rf $pkgdir/opt/${_pkgname}/steamapps
    $srcdir/linux/tools/find_interfaces.sh $pkgdir/opt/${_pkgname}/lib/libsteam_api.so > $pkgdir/opt/${_pkgname}/lib/steam_interfaces.txt || true
    cp $srcdir/linux/x86_64/libsteam_api.so $pkgdir/opt/${_pkgname}/lib/libsteam_api.so
    echo "${_steamid}" > $pkgdir/opt/${_pkgname}/lib/steam_appid.txt
    rm $pkgdir/opt/${_pkgname}/config/steam-check.sh

    # Install desktop file.
    install -D -m 644 $srcdir/${_pkgname}.desktop \
             $pkgdir/usr/share/applications/${_pkgname}.desktop

    # Install icon file.
    install -D -m 644 $srcdir/${_pkgname}/share/GameIcons/icon_512x512.png \
             $pkgdir/usr/share/pixmaps/${_pkgname}.png

    # Install bash startup script.
    install -D -m 755 $srcdir/${_pkgname}.sh \
             $pkgdir/usr/bin/${_pkgname}
}
